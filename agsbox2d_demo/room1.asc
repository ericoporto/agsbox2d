// room script file
DynamicSprite* dynspr;
DrawingSurface* surf;

World* world;

struct Physical {
Body* body;
Shape* shape;
Fixture* fixture;
};

Physical ground;
Physical ball;
Physical box1;
Physical box2;

void initPhysics(){
  AgsBox2D.SetMeter(32.0);
  world = AgsBox2D.CreateWorld(0.0, 9.8*AgsBox2D.GetMeter());
  
  ground.body = AgsBox2D.CreateBody(world, 160.0, 160.0, eBodyStatic);
  ground.shape = AgsBox2D.CreateRectangleShape(320.0, 40.0);
  ground.fixture = AgsBox2D.CreateFixture(ground.body, ground.shape);

  ball.body = AgsBox2D.CreateBody(world, 160.0, 40.0, eBodyDynamic);
  ball.shape = AgsBox2D.CreateCircleShape(20.0);
  ball.fixture = AgsBox2D.CreateFixture(ball.body, ball.shape, 1.0); 
  ball.fixture.Restitution = 0.5;
  
  box1.body = AgsBox2D.CreateBody(world, 80.0, 60.0, eBodyDynamic);
  box1.shape = AgsBox2D.CreateRectangleShape(30.0, 20.0);
  box1.fixture = AgsBox2D.CreateFixture(box1.body, box1.shape, 5.0);
  
  box2.body = AgsBox2D.CreateBody(world, 80.0, 80.0, eBodyDynamic);
  box2.shape = AgsBox2D.CreateRectangleShape(20.0, 20.0);
  box2.fixture = AgsBox2D.CreateFixture(box2.body, box2.shape, 2.0);
}

function room_AfterFadeIn()
{

}


function room_Load()
{
  initPhysics();
  gDebugGUI.Transparency = 50;
}

int WorldDraw(){
  if(dynspr!=null){
    dynspr.Delete();
    dynspr = null;
  }
  
  dynspr = DynamicSprite.Create(320, 180, true);
  surf = dynspr.GetDrawingSurface();
  
  //BLACK
  surf.DrawingColor = 0;
  surf.DrawRectangle(0, 0, 320, 180);
  
  //GREEN
  surf.DrawingColor = 4064;
  surf.DrawRectangle(ground.body.X-ground.shape.AsRectangle.Width/2, 
                     ground.body.Y-ground.shape.AsRectangle.Height/2,
                     ground.body.X+ground.shape.AsRectangle.Width/2, 
                     ground.body.Y+ground.shape.AsRectangle.Height/2);
  
  //RED
  surf.DrawingColor = 63808;
  surf.DrawCircle(ball.body.X, ball.body.Y, 20);
  
  //RED
  if(!box1.body.IsDestroyed){
    surf.DrawingColor = 63808;
    surf.DrawRectangle(box1.body.X-15, box1.body.Y-10, box1.body.X+15, box1.body.Y+10);
  }  
  
  //RED
  surf.DrawingColor = 63808;
  surf.DrawRectangle(box2.body.X-10, box2.body.Y-10, box2.body.X+10, box2.body.Y+10);
  
  surf.Release();
  return dynspr.Graphic;
}

function room_RepExec()
{
  gWorldGUI.BackgroundGraphic = WorldDraw();
  
  if(IsKeyPressed(eKeyLeftArrow)){
    ball.body.ApplyForce(-500.0, 0.0);
  }
  if(IsKeyPressed(eKeyRightArrow)){
    ball.body.ApplyForce(500.0, 0.0);
  }
  if(IsKeyPressed(eKeyUpArrow)){
    ball.body.ApplyForce(0.0, -5000.0);
    ball.body.SetLinearVelocity(0.0, 0.0);
  }
  if(IsKeyPressed(eKeyDownArrow)){
    ball.body.ApplyForce(0.0, 500.0);
  }
  
  if(!box1.body.IsDestroyed && ball.body.IsTouching(box1.body)){
    AgsBox2D.DestroyBody(world, box1.body);  
  }
    
  gDebugGUI.BackgroundGraphic = world.GetDebugSprite();
  world.Step(1.0/IntToFloat(GetGameSpeed()), 8, 3);
}
